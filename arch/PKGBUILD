# Maintainer: shmilee <shmilee.zju@gmail.com>

pkgname=gdpy3
pkgver=0.8.0
pkgrel=1
pkgdesc="Gyrokinetic Toroidal Code Data Processing tools written in python3"
url="https://github.com/shmilee/gdpy3.git"
arch=('any')
license=('MIT')
depends=('python-setuptools' 'python-numpy' 'python-scipy' 'python-matplotlib')
optdepends=('xz: for npz lzma compression method'
            'python-h5py: saving and loading hdf5 support'
            'python-paramiko: loading raw data through ssh'
            'tk: tkinter GUI support'
            'python-screeninfo>=0.4.1: tkinter GUI auto-choose screen support'
            'jupyter-notebook: GUI in Jupyter Notebook support'
            'python-ipywidgets: GUI in Jupyter Notebook support'
            'python-pillow: display graphics in terminal support'
            'libsixel: display graphics in terminal support')
makedepends=()
source=("https://github.com/shmilee/$pkgname/archive/v$pkgver.tar.gz" "gdpy3.desktop" "gdpy3-data.xml")
sha512sums=('SKIP'
            'cb4b615d385e04d94d927376e4da75db94e2aa5eb5dfd30192f2c7771465b21d9b6759c4864d42f52a076ef6f806ebdba391bf5b5c7c7bd0e079d0ced06f107b'
            'c5e3192f691c5fdc3855125f9c37b2501b29fc3fffb792208bbb4c7d2d8dfef439d4d8eb557e3f45ff2a235e368472ebc23154613d667231efd21ca0293f1197')

build() {
  if [ -d "$pkgname-$pkgver" ]; then
    cd $pkgname-$pkgver
  elif [ -d "${pkgname%-git}" ]; then
    cd ${pkgname%-git}
  fi
  python setup.py build
}

package() {
  if [ -d "$pkgname-$pkgver" ]; then
    cd $pkgname-$pkgver
  elif [ -d "${pkgname%-git}" ]; then
    cd ${pkgname%-git}
  fi
  python setup.py install --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
  pycmd="import src.__about__;  print(src.__about__.__icon_path__)"
  icon_path=$(python -c "$pycmd")
  install -Dm644 "$icon_path" "$pkgdir"/usr/share/icons/hicolor/128x128/apps/gdpy3.png
  install -Dm644 ../gdpy3.desktop "$pkgdir"/usr/share/applications/gdpy3.desktop
  install -Dm644 ../gdpy3-data.xml "$pkgdir"/usr/share/mime/packages/gdpy3-data.xml
}
