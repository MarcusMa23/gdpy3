# Maintainer: shmilee <shmilee.zju@gmail.com>

pkgname=gdpy3
pkgver=0.8.0
pkgrel=1
pkgdesc="Gyrokinetic Toroidal Code Data Processing tools written in python3"
url="https://github.com/shmilee/gdpy3.git"
arch=('any')
license=('MIT')
depends=('python-setuptools' 'python-numpy' 'python-scipy' 'python-matplotlib')
optdepends=('xz: for npz lzma compression method'
            'python-h5py: saving and loading hdf5 support'
            'python-paramiko: loading raw data through ssh'
            'tk: tkinter GUI support'
            'python-screeninfo>=0.4.1: tkinter GUI auto-choose screen support'
            'jupyter-notebook: GUI in Jupyter Notebook support'
            'python-ipywidgets: GUI in Jupyter Notebook support'
            'python-pillow: display graphics in terminal support'
            'libsixel: display graphics in terminal support')
makedepends=()
source=("https://github.com/shmilee/$pkgname/archive/v$pkgver.tar.gz" "gdpy3.desktop" "gdpy3-data.xml")
sha512sums=('SKIP'
            'a10239135e6ba9ed9b8064cc17712c6b2ac6cd2141f51d63c0a7ac829ec206a023814779d0b04bdd082d8d6c8ed7a76ef7bcbe2b5612dcc4db50afeecdd22437'
            '7df07baaebbb656fceec25b31c1d01ad2774f765878155e2f98df419f144d727f26728119aa2bf373b4690d8a111d2eebf679c812d9055da080ae966368c595f')

build() {
  if [ -d "$pkgname-$pkgver" ]; then
    cd $pkgname-$pkgver
  elif [ -d "${pkgname%-git}" ]; then
    cd ${pkgname%-git}
  fi
  python setup.py build
}

package() {
  if [ -d "$pkgname-$pkgver" ]; then
    cd $pkgname-$pkgver
  elif [ -d "${pkgname%-git}" ]; then
    cd ${pkgname%-git}
  fi
  python setup.py install --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
  pycmd="import src.__about__;  print(src.__about__.__icon_path__)"
  icon_path=$(python -c "$pycmd")
  install -Dm644 "$icon_path" "$pkgdir"/usr/share/icons/hicolor/128x128/apps/gdpy3.png
  install -Dm644 ../gdpy3.desktop "$pkgdir"/usr/share/applications/gdpy3.desktop
  install -Dm644 ../gdpy3-data.xml "$pkgdir"/usr/share/mime/packages/gdpy3-data.xml
}
