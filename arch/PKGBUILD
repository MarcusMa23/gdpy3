# Maintainer: shmilee <shmilee.zju@gmail.com>

pkgname=gdpy3
pkgver=0.3.6
pkgrel=1
pkgdesc="Gyrokinetic Toroidal Code Data Processing tools written in python3"
url="https://github.com/shmilee/gdpy3.git"
arch=('any')
license=('MIT')
depends=('python-setuptools' 'python-numpy' 'python-matplotlib')
optdepends=('python-h5py: for saving and loading hdf5 support'
            'python-paramiko: for loading raw data through ssh'
            'python-scipy: for smooth support'
            'tk: for GUI made by tkinter'
            'python-ipykernel: for GUI in Jupyter Notebook'
            'python-ipywidgets: for GUI in Jupyter Notebook')
makedepends=('git')
source=("https://github.com/shmilee/$pkgname/archive/v$pkgver.tar.gz" "gdpy3.desktop")
#source=("$pkgname-$pkgver.tar.gz" "gdpy3.desktop")
sha512sums=('SKIP'
            'a10239135e6ba9ed9b8064cc17712c6b2ac6cd2141f51d63c0a7ac829ec206a023814779d0b04bdd082d8d6c8ed7a76ef7bcbe2b5612dcc4db50afeecdd22437')

build() {
  cd $pkgname-$pkgver
  python setup.py build
}

package() {
  cd $pkgname-$pkgver
  python setup.py install --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
  install -Dm644 src/GUI/icon/main_128.png "$pkgdir"/usr/share/icons/hicolor/128x128/apps/gdpy3.png
  install -Dm644 ../gdpy3.desktop "$pkgdir"/usr/share/applications/gdpy3.desktop
}
